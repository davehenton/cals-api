import gov.ca.cwds.cals.persistence.model.calsns.rfa.*
import gov.ca.cwds.cals.persistence.model.calsns.dictionaries.*
import gov.ca.cwds.cals.service.dto.rfa.*
import gov.ca.cwds.cals.Constants
import gov.ca.cwds.cals.Constants.Validation.Business
import gov.ca.cwds.cals.Constants.Validation.Business.Code
import gov.ca.cwds.cals.Utils
import gov.ca.cwds.cals.exception.ValidationDetails;

global java.util.Set validationDetailsList;

rule "Validate Applicant's preferred phone"
  dialect "mvel"
  agenda-group "applicant-validation"
  when
    $applicant : ApplicantDTO();
    $actualPhone: PhoneDTO() from $applicant.phones;

    exists PhoneDTO(
      this != $actualPhone,
      $actualPhone.preferred,
      preferred
    ) from $applicant.phones
  then
    ValidationDetails details = new ValidationDetails();
    details.setCode(Code.APPLICANT_PREFERRED_PHONE);
    details.setUserMessage(Business.APPLICANT_PREFERRED_NUMBER_MESSAGE);
    details.setTechnicalMessage(Constants.BusinessRulesAgendaGroups.APPLICANT_VALIDATION);
    validationDetailsList.add(details);
end

rule "Validate Applicant phone numbers duplication"
  dialect "mvel"
  agenda-group "applicant-validation"
  when
      $applicant: ApplicantDTO()
      $phone: PhoneDTO() from $applicant.phones
      exists PhoneDTO(this.uuid != $phone.uuid, number == $phone.number, extension == $phone.extension) from $applicant.phones
  then
    ValidationDetails details = new ValidationDetails();
    details.setCode(Code.APPLICANT_PHONE_NUMBERS_DUPLICATION);
    details.setUserMessage(String.format(Constants.Validation.Business.APPLICANT_PHONE_NUMBERS_DUPLICATION_MESSAGE,
       Utils.Phone.formatNumber($phone),
              $applicant.getFirstName() != null ? $applicant.getFirstName() : "",
              $applicant.getLastName() != null ? $applicant.getLastName() : "",
              $applicant.getNameSuffix() != null ? $applicant.getNameSuffix().getValue() : ""));
    details.setTechnicalMessage(Constants.BusinessRulesAgendaGroups.APPLICANT_VALIDATION);
    validationDetailsList.add(details);
end
